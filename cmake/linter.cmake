if(UNIX OR FORCE_WINDOWS_LINTING)
  if (NOT ${CMAKE_VERSION} VERSION_LESS "3.6.0")
    set(ENABLE_CLANG_TIDY ON CACHE BOOL "Add clang-tidy automatically to builds")
    if (ENABLE_CLANG_TIDY)
      find_program (CLANG_TIDY_EXE NAMES "clang-tidy")
      if (CLANG_TIDY_EXE)
          message(STATUS "clang-tidy found: ${CLANG_TIDY_EXE}")
          if(UNIX)
            message(STATUS "Using UNIX clang-tidy configuration options.")
            set(CMAKE_CXX_CLANG_TIDY
                    "${CLANG_TIDY_EXE};-header-filter=${INCLUDES_DIR}/.*;-export-fixes=${CLANG_OUTPUT};-p ${CMAKE_BINARY_DIR}/compile_commands.json;--add-compiler-defaults")
          elseif(WIN32)
            message(STATUS "Using Windows clang-tidy configuration options.")
            set(CMAKE_CXX_CLANG_TIDY
                    "${CLANG_TIDY_EXE};-header-filter=${INCLUDES_DIR}/.*;-export-fixes=${CLANG_OUTPUT}")
          else()
            message(AUTHOR_WARNING "Using default clang-tidy options! Check your cmake/linter.cmake and add the ones you need!")
            set(CMAKE_CXX_CLANG_TIDY
                      "${CLANG_TIDY_EXE};-header-filter=${INCLUDES_DIR}/.*;-export-fixes=${CLANG_OUTPUT};-p ${CMAKE_BINARY_DIR}/compile_commands.json;")
          endif()
      else()
          message(AUTHOR_WARNING "clang-tidy not found!")
          set(CMAKE_CXX_CLANG_TIDY "" CACHE STRING "" FORCE)
      endif()
    else()
      message(AUTHOR_WARNING "clang-tidy is not enabled! Cmake variable ENABLE_CLANG_TIDY is set to: ${ENABLE_CLANG_TIDY}")
    endif()
  else()
    message(AUTHOR_WARNING "CMake Version is too low! Current version: ${CMAKE_VERSION}, supported: >= 3.6.0")
  endif()
elseif(WIN32)
  message(AUTHOR_WARNING "Clang-tidy does not work nicelly with MingGW. Set FORCE_WINDOWS_LINTING variable to ON if you want to use linting on Windows. 
  BE WARNED! Configuring Clang-tidy on windows can lead to configuration hell! Proceed if you are brave enought...
  Linter configuration can be found at ${PROJECT_SOURCE_DIR}/cmake/linter.cmake")
endif()